plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
}

def keystorePropertiesFile = rootProject.file("app/keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'ua.com.radiokot.photoprism'
    compileSdk 33

    defaultConfig {
        applicationId "ua.com.radiokot.photoprism"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        resConfigs "en"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        if (keystoreProperties['sessionId'] != null) {
            buildConfigField 'String', 'SESSION_ID', "\"" + keystoreProperties['sessionId'] + "\""
        } else {
            throw new GradleException("sessionId is required until there is auth in the app")
        }

        if (keystoreProperties['apiUrl'] != null) {
            buildConfigField 'String', 'API_URL', "\"" + keystoreProperties['apiUrl'] + "\""
        } else {
            throw new GradleException("apiUrl is required until there is an env selector in the app")
        }

        def fileProviderAuthority = applicationId + ".files"
        manifestPlaceholders.fileProviderAuthority = fileProviderAuthority
        buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', "\"" + fileProviderAuthority + "\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // Must be 2.13 for minSdk 21
    implementation ('com.fasterxml.jackson.core:jackson-databind:2.13.0')
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.0'

    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_android_version"

    implementation 'io.reactivex.rxjava3:rxjava:3.1.6'
    implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'

    implementation 'io.github.microutils:kotlin-logging:2.1.23'
    implementation 'com.gitlab.mvysny.slf4j:slf4j-handroid:2.0.4'

    implementation 'com.squareup.okhttp3:okhttp:4.9.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'

    implementation 'com.mikepenz:fastadapter:5.7.0'
    implementation 'com.mikepenz:fastadapter-extensions-scroll:5.7.0'

    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation "androidx.security:security-crypto:1.1.0-alpha05"

    testImplementation 'junit:junit:4.13.2'

    testImplementation "io.insert-koin:koin-test:$koin_version"
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"

    testImplementation 'org.slf4j:slf4j-simple:2.0.6'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}